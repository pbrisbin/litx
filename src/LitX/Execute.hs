{-# LANGUAGE DerivingVia #-}

module LitX.Execute
    ( ExecuteOptions
    , defaultExecuteOptions

    -- Prefer lens access because of the 'Endo'-based Options parsing
    , shebangL
    , bannerL
    , preambleL
    , commentLineL
    , languageL
    , executeModeL
    , executeArgsL
    , inheritEnvL
    , outputL
    , ExecuteMode(..)
    , InheritEnv(..)
    , Output(..)
    , executeMarkdown
    , setLanguageExecuteOptions
    ) where

import LitX.Prelude

import qualified Data.ByteString.Lazy as BSL
import qualified Data.Text as T
import Data.Version
import LitX.CodeBlock
import LitX.Language
import LitX.Parse (Markdown, markdownCodeBlocks)
import qualified Paths_litx as Pkg
import System.Process.Typed

data ExecuteOptions = ExecuteOptions
    { eoLanguage :: Language
    , eoShebang :: Text
    , eoBanner :: Maybe Text
    , eoPreamble :: Maybe Text
    , eoCommentLine :: Maybe Text -> Text
    , eoExecuteMode :: ExecuteMode
    , eoExecuteArgs :: [String]
    , eoInheritEnv :: InheritEnv
    , eoOutput :: Output
    }

defaultExecuteOptions :: Language -> ExecuteOptions
defaultExecuteOptions language =
    setLanguageExecuteOptions language $ ExecuteOptions
        { eoLanguage = error "Programmer error: Language unset"
        , eoShebang = "/usr/bin/env cat"
        , eoBanner = Just $ T.intercalate
            "\n"
            [ defaultCommentLine Nothing
            , defaultCommentLine $ Just generatedBy
            , defaultCommentLine Nothing
            ]
        , eoPreamble = Nothing
        , eoCommentLine = defaultCommentLine
        , eoExecuteMode = Execute "cat"
        , eoExecuteArgs = []
        , eoInheritEnv = InheritEnv
        , eoOutput = OutputFile "/dev/null"
        }

defaultCommentLine :: Maybe Text -> Text
defaultCommentLine = maybe "#" ("# " <>)

generatedBy :: Text
generatedBy = "Generated by litx-v" <> pack (showVersion Pkg.version) <> "."

languageL :: Lens' ExecuteOptions Language
languageL = lens eoLanguage $ \x y -> x { eoLanguage = y }

shebangL :: Lens' ExecuteOptions Text
shebangL = lens eoShebang $ \x y -> x { eoShebang = y }

bannerL :: Lens' ExecuteOptions (Maybe Text)
bannerL = lens eoBanner $ \x y -> x { eoBanner = y }

preambleL :: Lens' ExecuteOptions (Maybe Text)
preambleL = lens eoPreamble $ \x y -> x { eoPreamble = y }

commentLineL :: Lens' ExecuteOptions (Maybe Text -> Text)
commentLineL = lens eoCommentLine $ \x y -> x { eoCommentLine = y }

executeModeL :: Lens' ExecuteOptions ExecuteMode
executeModeL = lens eoExecuteMode $ \x y -> x { eoExecuteMode = y }

executeArgsL :: Lens' ExecuteOptions [String]
executeArgsL = lens eoExecuteArgs $ \x y -> x { eoExecuteArgs = y }

inheritEnvL :: Lens' ExecuteOptions InheritEnv
inheritEnvL = lens eoInheritEnv $ \x y -> x { eoInheritEnv = y }

outputL :: Lens' ExecuteOptions Output
outputL = lens eoOutput $ \x y -> x { eoOutput = y }

data ExecuteMode
    = Execute String
    | NoExecute

data InheritEnv
    = InheritEnv
    | Don'tInheritEnv

data Output
    = OutputStdout
    | OutputFile FilePath

instance IsString Output where
    fromString = \case
        "-" -> OutputStdout
        path -> OutputFile path

executeMarkdown :: MonadUnliftIO m => ExecuteOptions -> Markdown -> m ()
executeMarkdown ExecuteOptions {..} markdown = do
    case eoOutput of
        OutputStdout -> putStr script
        OutputFile path -> writeFile path script

    case eoExecuteMode of
        Execute cmd -> do
            let input = byteStringInput $ BSL.fromStrict $! encodeUtf8 script
            runProcess_ $ clearEnv $ setStdin input $ proc cmd eoExecuteArgs
        NoExecute -> pure ()
  where
    script = mconcat
        [ "#!" <> eoShebang <> "\n"
        , maybe "" (<> "\n") eoBanner
        , maybe "" (<> "\n\n") eoPreamble
        , renderCodeBlocks
        ]

    clearEnv = case eoInheritEnv of
        InheritEnv -> id
        Don'tInheritEnv -> setEnv []

    renderCodeBlocks :: Text
    renderCodeBlocks =
        T.intercalate "\n"
            $ map renderCodeBlock
            $ filter ((== eoLanguage) . codeBlockLanguage)
            $ markdownCodeBlocks markdown

    renderCodeBlock :: CodeBlock -> Text
    renderCodeBlock block =
        eoCommentLine (Just $ sourceAnnotation block) <> codeBlockContent block

    sourceAnnotation :: CodeBlock -> Text
    sourceAnnotation block =
        "source="
            <> pack (codeBlockPath block)
            <> maybe "" ((":" <>) . pack . show) (codeBlockLine block)
            <> "\n"

setLanguageExecuteOptions :: Language -> ExecuteOptions -> ExecuteOptions
setLanguageExecuteOptions = \case
    Bash ->
        (languageL .~ Bash)
            . (shebangL .~ "/usr/bin/env bash")
            . (bannerL ?~ ("#\n# " <> generatedBy <> "\n#\n###"))
            . (preambleL ?~ "set -euo pipefail")
            . (commentLineL .~ defaultCommentLine)
            . (executeModeL .~ Execute "bash")
            . (executeArgsL .~ ["-s", "-"])
